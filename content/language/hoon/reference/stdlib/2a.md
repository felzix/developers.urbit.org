+++
title = "2a: Unit Logic"
weight = 4

[glossaryEntry."Unit as argument"]
name = "Unit as argument"
symbol = "biff"
usage = "stdlib"
slug = "#biff"
desc = "Used in the Hoon standard library."

[glossaryEntry."Non-unit function to unit, producing unit"]
name = "Non-unit function to unit, producing unit"
symbol = "bind"
usage = "stdlib"
slug = "#bind"
desc = "Used in the Hoon standard library."

[glossaryEntry."Replace null"]
name = "Replace null"
symbol = "bond"
usage = "stdlib"
slug = "#bond"
desc = "Used in the Hoon standard library."

[glossaryEntry."Group unit values into pair"]
name = "Group unit values into pair"
symbol = "both"
usage = "stdlib"
slug = "#both"
desc = "Used in the Hoon standard library."

[glossaryEntry."Combine two units with function"]
name = "Combine two units with function"
symbol = "clap"
usage = "stdlib"
slug = "#clap"
desc = "Used in the Hoon standard library."

[glossaryEntry."Compose two units with function"]
name = "Compose two units with function"
symbol = "clef"
usage = "stdlib"
slug = "#clef"
desc = "Used in the Hoon standard library."

[glossaryEntry."Unit to list"]
name = "Unit to list"
symbol = "drop"
usage = "stdlib"
slug = "#drop"
desc = "Used in the Hoon standard library."

[glossaryEntry."Give unit a default value"]
name = "Give unit a default value"
symbol = "fall"
usage = "stdlib"
slug = "#fall"
desc = "Used in the Hoon standard library."

[glossaryEntry."Make filter"]
name = "Make filter"
symbol = "flit"
usage = "stdlib"
slug = "#flit"
desc = "Used in the Hoon standard library."

[glossaryEntry."Select between two units by a rule"]
name = "Select between two units by a rule"
symbol = "hunt"
usage = "stdlib"
slug = "#hunt"
desc = "Used in the Hoon standard library."

[glossaryEntry."Curried bind"]
name = "Curried bind"
symbol = "lift"
usage = "stdlib"
slug = "#lift"
desc = "Used in the Hoon standard library."

[glossaryEntry.Choose]
name = "Choose"
symbol = "mate"
usage = "stdlib"
slug = "#mate"
desc = "Used in the Hoon standard library."

[glossaryEntry."Unwrap unit"]
name = "Unwrap unit"
symbol = "need"
usage = "stdlib"
slug = "#need"
desc = "Used in the Hoon standard library."

[glossaryEntry."Wrap value in unit"]
name = "Wrap value in unit"
symbol = "some"
usage = "stdlib"
slug = "#some"
desc = "Used in the Hoon standard library."

+++

## `++biff`

[`Unit`](/reference/hoon/stdlib/1c#unit) as argument

Applies a function `b` that produces a [`unit`](/reference/hoon/stdlib/1c#unit) to the unwrapped value of [`unit`](/reference/hoon/stdlib/1c#unit)
`a` (`u.a`). If `a` is empty, `~` is produced.

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

`b` is a function that accepts a [`noun`](/reference/glossary/noun) and produces a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Produces

A [`unit`](/reference/hoon/stdlib/1c#unit).

#### Source

```hoon
++  biff
  |*  [a=(unit) b=$-(* (unit))]
  ?~  a  ~
  (b u.a)
```

#### Examples

```
> (biff (some 5) |=(a=@ (some (add a 2))))
[~ u=7]
```

```
> (biff ~ |=(a=@ (some (add a 2))))
~
```

---

## `++bind`

Non-unit function to [`unit`](/reference/hoon/stdlib/1c#unit), producing [`unit`](/reference/hoon/stdlib/1c#unit)

Applies a function `b` to the value (`u.a`) of a [`unit`](/reference/hoon/stdlib/1c#unit) `a`, producing
a [`unit`](/reference/hoon/stdlib/1c#unit). Used when you want a function that does not accept or produce a
[`unit`](/reference/hoon/stdlib/1c#unit) to both accept and produce a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

`b` is a function.

#### Produces

A [`unit`](/reference/hoon/stdlib/1c#unit).

#### Source

```hoon
++  bind
  |*  [a=(unit) b=gate]
  ?~  a  ~
  [~ u=(b u.a)]
```

#### Examples

```
> (bind ((unit @) [~ 97]) ,@t)
[~ u='a']
```

```
> =a |=(a=@ (add a 1))
> (bind ((unit @) [~ 2]) a)
[~ u=3]
```

---

## `++bond`

Replace null

Replaces an empty [`unit`](/reference/hoon/stdlib/1c#unit) `b` with the product of a called [`trap`](/reference/glossary/trap)
`a`. If the [`unit`](/reference/hoon/stdlib/1c#unit) is not empty, then the original [`unit`](/reference/hoon/stdlib/1c#unit) is produced.

#### Accepts

`a` is a [`trap`](/reference/glossary/trap).

`b` is a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Produces

Either the product of `a` or the value inside of [`unit`](/reference/hoon/stdlib/1c#unit) `b`.

#### Source

```hoon
++  bond
  |*  a=(trap)
  |*  b=(unit)
  ?~  b  $:a
  u.b
```

#### Examples

```
    > (bex 10)
    1.024
    > ((bond |.((bex 10))) ~)
    1.024
```

```
    > ((bond |.((bex 10))) (slaw %ud '123'))
    123
```

---

## `++both`

Group [`unit`](/reference/hoon/stdlib/1c#unit) values into pair

Produces `~` if either `a` or `b` are empty. Otherwise, produces a
[`unit`](/reference/hoon/stdlib/1c#unit) whose value is a cell of the values of two input [`units`](/reference/hoon/stdlib/1c#unit) `a` and
`b`.

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

`b` is a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Produces

A [`unit`](/reference/hoon/stdlib/1c#unit) of the two initial values.

#### Source

```hoon
++  both
  |*  [a=(unit) b=(unit)]
  ?~  a  ~
  ?~  b  ~
  [~ u=[u.a u.b]]
```

#### Examples

```
> (both (some 1) (some %b))
[~ u=[1 %b]]
```

```
> (both ~ (some %b))
~
```

---

## `++clap`

Combine two [`units`](/reference/hoon/stdlib/1c#unit) with function

Applies a binary function `c`--which does not usually accept or produce a
[`unit`](/reference/hoon/stdlib/1c#unit)-- to the values of two [`units`](/reference/hoon/stdlib/1c#unit), `a` and `b`, producing a [`unit`](/reference/hoon/stdlib/1c#unit).

If `a` is null, produces `b`. If `b` is null, produces `a`. Otherwise, produces the produce of `c` in a `unit`.

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

`b` is a [`unit`](/reference/hoon/stdlib/1c#unit).

`c` is a function that performs a binary operation.

#### Produces

A [`unit`](/reference/hoon/stdlib/1c#unit).

#### Source

```hoon
++  clap
  |*  [a=(unit) b=(unit) c=_=>(~ |=(^ +<-))]
  ?~  a  b
  ?~  b  a
  [~ u=(c u.a u.b)]
```

#### Examples

```
> (clap ~ `'b' |=([a=@tD b=@tD] `tape`[a b ~]))
[~ u='b']
```

```
> (clap `'a' ~ |=([a=@tD b=@tD] `tape`[a b ~]))
[~ u='a']
```

```
> (clap `'a' `'b' |=([a=@tD b=@tD] `tape`[a b ~]))
[~ u="ab"]
```

---

## `++clef`

Compose two [`units`](/reference/hoon/stdlib/1c#unit) with function

Applies a binary function `c`--which does not usually accept [`units`](/reference/hoon/stdlib/1c#unit)-- to the values of two [`units`](/reference/hoon/stdlib/1c#unit), `a` and `b`, producing a [`unit`](/reference/hoon/stdlib/1c#unit).

If `a` or `b` are null, produces null. Otherwise, produces the produce of `c`.

#### Accepts

`a` is a `unit`.

`b` is a `unit`.

`c` is a `gate` that takes a `cell` and produces a `unit`.

#### Produces

A `unit`.

#### Source

```hoon
++  clef
  |*  [a=(unit) b=(unit) c=_=>(~ |=(^ `+<-))]
  ?~  a  ~
  ?~  b  ~
  (c u.a u.b)
```

#### Examples

```
> (clef ~ `'b' |=([a=@tD b=@tD] `(unit tape)`[~ [a b ~]]))
~
```

```
> (clef `'a' ~ |=([a=@tD b=@tD] `(unit tape)`[~ [a b ~]]))
~
```

```
> (clef `'a' `'b' |=([a=@tD b=@tD] `(unit tape)`[~ [a b ~]]))
[~ "ab"]
```

---

## `++drop`

[`Unit`](/reference/hoon/stdlib/1c#unit) to list

Makes a [`++list`](/reference/hoon/stdlib/1c#list) of the unwrapped value (`u.a`) of a [`unit`](/reference/hoon/stdlib/1c#unit) `a`.

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Produces

A list.

#### Source

```hoon
++  drop
  |*  a=(unit)
  ?~  a  ~
  [i=u.a t=~]
```

#### Examples

```
> =a ((unit @) [~ 97])
> (drop a)
[i=97 t=~]
```

```
> =a ((unit @) [~])
> (drop a)
~
```

---

## `++fall`

Give [`unit`](/reference/hoon/stdlib/1c#unit) a default value

Produces a default value `b` for a [`unit`](/reference/hoon/stdlib/1c#unit) `a` in cases where `a` is null.

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

`b` is a [`noun`](/reference/glossary/noun) that's used as the default value.

#### Produces

Either a [`noun`](/reference/glossary/noun) `b` or the unwrapped value of [`unit`](/reference/hoon/stdlib/1c#unit) `a`.

#### Source

```hoon
++  fall
  |*  [a=(unit) b=*]
  ?~(a b u.a)
```

#### Examples

```
> (fall ~ 'a')
'a'
> (fall [~ u=0] 'a')
0
```

---

## `++flit`

Make filter

Accepts gate `a` which produces a `?`. Applies `a` to `b`, producing `b` wrapped in a `unit` if true, otherwise produces null.

`+flit` is a wet gate that takes `a` and produces a new wet gate that takes `b`.

#### Accepts

`a` is a `gate` which produces `?`, and is the sample of the outer wet gate.

`b` is any `noun`, and is the sample of the inner wet gate.

#### Produces

`(unit [type])`, where `[type]` is the type of `b`.

#### Source

```hoon
++  flit
  |*  a=$-(* ?)
  |*  b=*
  ?.((a b) ~ [~ u=b])
```

#### Examples

```
> =f (flit |=(=@ud ?:(=(ud 1) %.y %.n)))
> (f 1)
[~ 1]
> (f 2)
~
```

---

## `++hunt`

First of units

Apply binary gate `ord`, which produces a `?`, to units `a` and `b`. Produce `a` if true and `b` if false. If `a` is null, produce `b`. If `b` is null, produce `a`.

This allows selecting between two units by some rule.

#### Accepts

`ord` is a `$-(^ ?)` - a binary gate that produces `?`.

`a` is a `unit`.

`b` is a `unit`.

#### Produces

A `unit`.

#### Source

```hoon
++  hunt
  |*  [ord=$-(^ ?) a=(unit) b=(unit)]
  ^-  %-  unit
      $?  _?>(?=(^ a) u.a)
          _?>(?=(^ b) u.b)
      ==
  ?~  a  b
  ?~  b  a
  ?:((ord u.a u.b) a b)
```

#### Examples

```
> (hunt gte ~ `20)
[~ 20]
```

```
> (hunt gte `10 ~)
[~ 10]
```

```
> (hunt gte `10 `20)
[~ 20]
```

---

## `++lift`

Curried bind

Accepts function `a` and produces a function that accepts [`unit`](/reference/hoon/stdlib/1c#unit)
`b` to which it applies `a`. Used when you want a function that does not accept
or produce a [`unit`](/reference/hoon/stdlib/1c#unit) to both accept and produce a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Accepts

`a` is a `mold`.

`b` is a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Produces

A [`unit`](/reference/hoon/stdlib/1c#unit).

#### Source

```hoon
++  lift
  |*  a=mold
  |*  b=(unit)
  (bind b a)
```

#### Examples

```
> ((lift dec) `(unit @)`~)
~
```

```
> ((lift dec) `(unit @)`[~ 20])
[~ 19]
```

---

## `++mate`

Choose

Accepts two [`units`](/reference/hoon/stdlib/1c#unit) `a` and `b` whose values are expected to be
equivalent. If either is empty, then the value of the other is produced.
If neither are empty, it asserts that both values are the same and
produces that value. If the assertion fails, `++mate` crashes with
`'mate'` in the stack trace.

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

`b` is a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Produces

A [`unit`](/reference/hoon/stdlib/1c#unit) or crash.

#### Source

```hoon
++  mate
  |*  [a=(unit) b=(unit)]
  ?~  b  a
  ?~  a  b
  ?.(=(u.a u.b) ~>(%mean.'mate' !!) a)
```

#### Examples

```
> =a ((unit @) [~ 97])
> =b ((unit @) [~ 97])
> (mate a b)
[~ 97]
```

```
> =a ((unit @) [~ 97])
> =b ((unit @) [~])
> (mate a b)
[~ 97]
```

```
> =a ((unit @) [~ 97])
> =b ((unit @) [~ 98])
> (mate a b)
! 'mate'
! exit
```

---

## `++need`

Unwrap [`unit`](/reference/hoon/stdlib/1c#unit)

Retrieve the value from a [`unit`](/reference/hoon/stdlib/1c#unit) and crash if the [`unit`](/reference/hoon/stdlib/1c#unit) is null.

#### Accepts

`a` is a [`unit`](/reference/hoon/stdlib/1c#unit).

#### Produces

Either the unwrapped value of `a` (`u.a`), or crash.

#### Source

```hoon
++  need
  ~/  %need
  |*  a=(unit)
  ?~  a  ~>(%mean.'need' !!)
  u.a
```

#### Examples

```
> =a ((unit [@t @t]) [~ ['a' 'b']])
> (need a)
['a' 'b']
```

```
> =a ((unit @ud) [~ 17])
> (need a)
17
```

```
> =a ((unit @) [~])
> (need a)
! exit
```

---

## `++some`

Wrap value in a [`unit`](/reference/hoon/stdlib/1c#unit)

Takes any [`noun`](/reference/glossary/noun) `a` and produces a [`unit`](/reference/hoon/stdlib/1c#unit) with the value set to `a`.

#### Accepts

`a` is a [`noun`](/reference/glossary/noun).

#### Produces

A [`unit`](/reference/hoon/stdlib/1c#unit).

#### Source

```hoon
++  some
  |*  a=*
  [~ u=a]
```

#### Examples

```
> (some ['a' 'b'])
[~ u=['a' 'b']]
```

```
> (some &)
[~ u=%.y]
```

---
